cmake_minimum_required (VERSION 2.8)
project (sample_assignment)

###################################################################################

# these variables will be replaced by INSTALL.sh
set (HSS_INSTALL_DIR __INSTALL__FILLIN__HSS_INSTALL_DIR__)
set (HSS_DATA_DIR __INSTALL__FILLIN__HSS_DATA_DIR__)

# assuming the core files for config, compilation, running, & grading are here:
set (GRADINGCODE  ${HSS_INSTALL_DIR}/src)
set (GRADINGLIB   ${HSS_INSTALL_DIR}/src/grading/lib/libsubmitty_grading.a)

###################################################################################

# assuming we are in a directory of this structure
#   HSS_DATA_DIR/courses/which_semester/which_course/build/which_assignment

set (MY_DIR ${CMAKE_SOURCE_DIR})

# base directory for course
set (course_dir ${MY_DIR}/../..)
#alternately/equivalently
#set (course_dir ${HSS_DATA_DIR}/courses)


# just the last part of the current directory
get_filename_component(assignment ${MY_DIR} NAME)
message("ASSIGNMENT IS ${assignment}")

# Where the compiled binaries are stored, under the class directory
set(my_bin ${course_dir}/bin/${assignment})

# Assignment configuration json file location
set(my_hw_config_file ${course_dir}/config/${assignment}_assignment_config.json)


###################################################################################
# OPTIONAL INSTRUCTOR CUSTOMIZATION 
#
# If extra .cpp files are needed for grading/validation, add a file
#  named "instructor_CMakeLists.txt" and in that file list the .cpp
#  files in the variable "custom_files".  For example:
#
#  set (custom_files   test_code/custom.cpp  test_code/helper.cpp)
#

include(instructor_CMakeLists.txt OPTIONAL RESULT_VARIABLE instructor_cmakelists)

if (instructor_cmakelists)
message("DETECTED INSTRUCTOR CUSTOMIZATIONS in ${instructor_cmakelists}")
message("  custom_files: ${custom_files}")
endif(instructor_cmakelists)


###################################################################################
# source files

# build this as a library, so we only have to compile it once
add_library(seccomp_functions
  ${GRADINGCODE}/grading/seccomp_functions.cpp 
)

add_executable(configure.out
  ${GRADINGCODE}/grading/main_configure.cpp 
  ${custom_files}  
)

add_executable(compile.out
  ${GRADINGCODE}/grading/main_compile.cpp 
  ${custom_files}
)

add_executable(run.out
  ${GRADINGCODE}/grading/main_runner.cpp 
  ${custom_files}
)

add_executable(validate.out
  ${GRADINGCODE}/grading/main_validator.cpp 
  ${custom_files}
)

###################################################################################

target_link_libraries(seccomp_functions seccomp)
set_property(TARGET seccomp_functions APPEND_STRING PROPERTY COMPILE_FLAGS "-g -std=c++11")

target_link_libraries(configure.out  ${GRADINGLIB}  seccomp  seccomp_functions)
target_link_libraries(compile.out    ${GRADINGLIB}  seccomp  seccomp_functions)
target_link_libraries(run.out        ${GRADINGLIB}  seccomp  seccomp_functions)
target_link_libraries(validate.out   ${GRADINGLIB}  seccomp  seccomp_functions)

set_property(TARGET configure.out APPEND_STRING PROPERTY COMPILE_FLAGS "-g -std=c++11")
set_property(TARGET compile.out   APPEND_STRING PROPERTY COMPILE_FLAGS "-g -std=c++11")
set_property(TARGET run.out       APPEND_STRING PROPERTY COMPILE_FLAGS "-g -std=c++11")
set_property(TARGET validate.out  APPEND_STRING PROPERTY COMPILE_FLAGS "-g -std=c++11")

include_directories(${MY_DIR})
include_directories(${GRADINGCODE})

SET(EXECUTABLE_OUTPUT_PATH ${my_bin})

add_custom_target(config_tgt ALL DEPENDS ${my_hw_config_file})
add_custom_command(OUTPUT ${my_hw_config_file} DEPENDS configure.out COMMAND ${my_bin}/configure.out ${my_hw_config_file})

###################################################################################


