cmake_minimum_required (VERSION 2.8)
project (sample_assignment)

###################################################################################

# these variables will be replaced by INSTALL.sh
set (SUBMITTY_INSTALL_DIR __INSTALL__FILLIN__SUBMITTY_INSTALL_DIR__)
set (SUBMITTY_DATA_DIR __INSTALL__FILLIN__SUBMITTY_DATA_DIR__)

# assuming the core files for config, compilation, running, & grading are here:
set (GRADINGCODE  ${SUBMITTY_INSTALL_DIR}/src)
set (GRADINGLIB   ${SUBMITTY_INSTALL_DIR}/src/grading/lib/libsubmitty_grading.a)

###################################################################################

# DEFAULT:  NORMAL ASSIGNMENT INSTALLATION
# NON-DEFAULT:  TEST SUITE INSTALLATION
option(ASSIGNMENT_INSTALLATION "Normal assignment installation" ON)

set (MY_DIR ${CMAKE_SOURCE_DIR})

IF (ASSIGNMENT_INSTALLATION)
  # assuming we are in a directory of this structure
  #   SUBMITTY_DATA_DIR/courses/which_semester/which_course/build/which_assignment

  # base directory for course
  set (course_dir ${MY_DIR}/../..)
  #alternately/equivalently
  #set (course_dir ${SUBMITTY_DATA_DIR}/courses)

  # just the last part of the current directory
  get_filename_component(assignment ${MY_DIR} NAME)
  message("ASSIGNMENT IS ${assignment}")

  set (my_config_json ${course_dir}/build/${assignment}/config.json)

  # Where the compiled binaries are stored, under the class directory
  set(my_bin ${course_dir}/bin/${assignment})

  # Assignment configuration json file location
  set(my_hw_config_file ${course_dir}/config/${assignment}_assignment_config.json)

else (ASSIGNMENT_INSTALLATION)
  # Otherwise, this is part of the test suite....
  # base directory for test suite is the immediate parent directory
  get_filename_component(which_test_dir ${MY_DIR} DIRECTORY)

  # the name of the test is the name of the parent directory
  get_filename_component(which_test ${which_test_dir} NAME)
  message("WHICH TEST: ${which_test}")
  
  include_directories(${which_test_dir}/assignment_config)

  set (my_config_json ${which_test_dir}/assignment_config/config.json)

  # Where the compiled binaries are stored
  set(my_bin ${which_test_dir}/bin)
  message("this is where i'll put the executables ${my_bin}")

  # configuration json file location
  set(my_hw_config_file ${which_test_dir}/data/assignment_config.json)

endif(ASSIGNMENT_INSTALLATION)


###################################################################################
# OPTIONAL INSTRUCTOR CUSTOMIZATION 
#
# If extra .cpp files are needed for grading/validation, add a file
#  named "instructor_CMakeLists.txt" and in that file list the .cpp
#  files in the variable "custom_files".  For example:
#
#  set (custom_files   test_code/custom.cpp  test_code/helper.cpp)
#

include(instructor_CMakeLists.txt OPTIONAL RESULT_VARIABLE instructor_cmakelists)

if (instructor_cmakelists)
message("DETECTED INSTRUCTOR CUSTOMIZATIONS in ${instructor_cmakelists}")
message("  custom_files: ${custom_files}")
endif(instructor_cmakelists)


###################################################################################
# embed the config.json within a generated .cpp source file

file(READ ${my_config_json} json_generated)

# escape " and \ and newline
string(REGEX REPLACE "([\"\\\n])" "\\\\\\1" json_generated "${json_generated}")

set(cpp_header "const char *GLOBAL_config_json_string = \"")
set(cpp_footer "\";\n")
file(WRITE json_generated.cpp "${cpp_header}${json_generated}${cpp_footer}")
set_source_files_properties(json_generated.cpp PROPERTIES GENERATED true)


###################################################################################
# source files

# build this as a library, so we only have to compile it once
add_library(custom_functions
  ${GRADINGCODE}/grading/seccomp_functions.cpp  
  json_generated.cpp
  ${custom_files}  
)

add_executable(configure.out
  ${GRADINGCODE}/grading/main_configure.cpp 
)

add_executable(compile.out
  ${GRADINGCODE}/grading/main_compile.cpp 
)

add_executable(run.out
  ${GRADINGCODE}/grading/main_runner.cpp 
)

add_executable(validate.out
  ${GRADINGCODE}/grading/main_validator.cpp 
)


###################################################################################

target_link_libraries(custom_functions seccomp)
set_property(TARGET custom_functions APPEND_STRING PROPERTY COMPILE_FLAGS "-g -std=c++11")

target_link_libraries(configure.out  ${GRADINGLIB}  seccomp  custom_functions)
target_link_libraries(compile.out    ${GRADINGLIB}  seccomp  custom_functions)
target_link_libraries(run.out        ${GRADINGLIB}  seccomp  custom_functions)
target_link_libraries(validate.out   ${GRADINGLIB}  seccomp  custom_functions)

set_property(TARGET configure.out APPEND_STRING PROPERTY COMPILE_FLAGS "-g -std=c++11")
set_property(TARGET compile.out   APPEND_STRING PROPERTY COMPILE_FLAGS "-g -std=c++11")
set_property(TARGET run.out       APPEND_STRING PROPERTY COMPILE_FLAGS "-g -std=c++11")
set_property(TARGET validate.out  APPEND_STRING PROPERTY COMPILE_FLAGS "-g -std=c++11")

include_directories(${MY_DIR})
include_directories(${GRADINGCODE})

SET(EXECUTABLE_OUTPUT_PATH ${my_bin})

add_custom_target(config_tgt ALL DEPENDS ${my_hw_config_file})
add_custom_command(OUTPUT ${my_hw_config_file} DEPENDS configure.out COMMAND ${my_bin}/configure.out ${my_hw_config_file})

###################################################################################


